#version 320 es
precision highp float;

in vec2 v_texCoord;
uniform sampler2D u_texture;
uniform vec2 u_resolution;
uniform float u_zoom;
uniform float u_vignetteEnabled;

// Режимы шейдера: 0 = обычный, 1 = ч/б, 2 = инверсия, 3 = сепия
uniform int u_shaderMode;

out vec4 fragColor;

void main() {
    vec4 textureColor = texture(u_texture, v_texCoord) * 1.4;

    // размер одного пикселя
    vec2 texel = 1.0 / u_resolution;

    // 4 сэмпла (Sobel)
    float p00 = dot(texture(u_texture, v_texCoord - texel).rgb, vec3(0.299, 0.587, 0.114));
    float p11 = dot(texture(u_texture, v_texCoord + texel).rgb, vec3(0.299, 0.587, 0.114));
    float p10 = dot(texture(u_texture, v_texCoord + vec2(texel.x, -texel.y)).rgb, vec3(0.299, 0.587, 0.114));
    float p01 = dot(texture(u_texture, v_texCoord + vec2(-texel.x, texel.y)).rgb, vec3(0.299, 0.587, 0.114));

    float gx = p00 - p11;
    float gy = p10 - p01;

    float edge = length(vec2(gx, gy));
    edge = smoothstep(0.0, u_zoom, edge);

    vec4 background = vec4(1.0, 0.969, 0.855, 1.0);

    vec4 finalBackground = background;
    vec4 textureMixBackground = mix(finalBackground, textureColor, 0.1875);

    float gray = (textureColor.r + textureColor.g + textureColor.b) / 3.0;
    gray = step(0.06, gray);

    vec4 result = mix(finalBackground, textureMixBackground, gray);

    float white = 1.0 - edge;

    //Lerp white
    float p1 = 0.0;
    float p2 = 0.98;
    vec4 color1 = vec4(0.678, 0.569, 0.435, 1.0);
    vec4 color2 = vec4(1.0);
    vec4 color = mix(color1, color2, (white - p1) / (p2 - p1));

    vec4 colorA = color * result;
    vec4 pastelColor = colorA;

    // Применяем эффекты в зависимости от режима
    vec4 finalColor = pastelColor;
    
    if (u_shaderMode == 1) {
        // Чёрно-белый режим
        float lum = dot(pastelColor.rgb, vec3(0.299, 0.587, 0.114));
        finalColor = vec4(vec3(lum), pastelColor.a);
    } else if (u_shaderMode == 2) {
        // Инверсия
        finalColor = vec4(1.0 - pastelColor.rgb, pastelColor.a);
    } else if (u_shaderMode == 3) {
        // Сепия
        float lum = dot(pastelColor.rgb, vec3(0.299, 0.587, 0.114));
        vec3 sepiaColor = vec3(lum * 1.2, lum * 1.0, lum * 0.8);
        finalColor = vec4(sepiaColor, pastelColor.a);
    }

    // --- ВИНЬЕТКА ---
    vec2 uv = v_texCoord;
    vec2 pos = uv * 2.0 - 1.0;
    float aspect = u_resolution.x / u_resolution.y;
    pos.x *= aspect;
    float dist = length(pos);
    float maxDist = length(vec2(aspect, 1.0));
    float normDist = dist / maxDist;
    float radius = 0.9;
    float softness = 0.8;
    float vignette = smoothstep(radius, radius - softness, normDist);

    float vignetteFactor = mix(1.0, vignette, u_vignetteEnabled);

    fragColor = finalColor * vignetteFactor;
}
